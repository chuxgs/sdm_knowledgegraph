#### -------------- Query 1 -------------- ####

# Find all Authors
# Assuming we want to return both authors' ids and names

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?author ?name
WHERE {
  ?author rdf:type lab2:Author .
  ?author lab2:name_author ?name .
}

#### -------------- Query 2 -------------- ####

# Find all properties whose domain is Author

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?property
WHERE {
    ?property rdf:type rdf:Property .
    ?property rdfs:domain lab2:Author .
}

#### -------------- Query 3 -------------- ####

# Find all properties whose domain is either Conference or Journal.

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>


SELECT ?property
WHERE {
  {
    ?property rdfs:domain lab2:Edition .
    FILTER NOT EXISTS {
      ?property rdfs:label ?propertyName .
      FILTER (CONTAINS(UCASE(?propertyName), "WORKSHOP"))
    }
  }
  UNION
  {
    ?property rdfs:domain lab2:Journal
  }
}

OR

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?property
WHERE {
  {
    ?property rdfs:domain lab2:Edition .
    ?property rdfs:label ?propertyName .
    FILTER (CONTAINS(UCASE(?propertyName), "CONFERENCE"))
  }
  UNION
  {
    ?property rdfs:domain lab2:Journal
  }
}


#### -------------- Query 4 -------------- ####

# Find all the papers written by Di Diu that where published in database conferences
# Assuming our author's name is Di Liu

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT (?pTitle AS ?paper_title) (?confName AS ?conferenceName)
WHERE
{
    ?paper rdf:type lab2:Paper .
    ?paper lab2:title ?pTitle .
    ?paper lab2:published_conference ?conference .
    ?paper lab2:keyword ?keyword .

    ?keyword rdf:type lab2:Keyword .
    ?keyword lab2:name_keyword "database" .

    ?author rdf:type lab2:Author .
    ?author lab2:name_author "Di Liu" .
    ?author lab2:author ?paper .

    ?conference lab2:conference ?confName .
}

#### -------------- Query 5 -------------- ####

# Find all the authors whose papers were never rejected

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?author ?authorname ?papertitle
WHERE
{
    ?author rdf:type lab2:Author .
    ?paper rdf:type lab2:Paper .
    ?review rdf:type lab2:Review .
    ?author lab2:author ?paper .
    ?review lab2:applied_to ?paper .
    ?review lab2:decision "Accepted" .
    ?author lab2:name_author ?authorname .
    ?paper lab2:title ?papertitle .

    MINUS {
        ?rejectedPaper rdf:type lab2:Paper .
        ?rejectedReview rdf:type lab2:Review .
        ?author lab2:author ?rejectedPaper .
        ?rejectedReview lab2:applied_to ?rejectedPaper .
        ?rejectedReview lab2:decision "Rejected" .
    }
}

#### -------------- Query 6 -------------- ####

# Return the count of rejected papers for each university in a decreasing order

PREFIX lab2: <http://sdmlab2.org>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?universities ?universitiesnames (COUNT(?papers) AS ?rejectedPapersCount)
WHERE {
    ?authors rdf:type lab2:Author .
    ?universities rdf:type lab2:University .
    ?papers rdf:type lab2:Paper .
    ?reviews rdf:type lab2:Review .
    ?authors lab2:university ?universities .
    ?universities lab2:name_university ?universitiesnames .
    ?authors lab2:author ?papers .
    ?reviews lab2:applied_to ?papers .
    ?reviews lab2:decision "Rejected" .
}
GROUP BY ?universities ?universitiesnames
ORDER BY DESC(?rejectedPapersCount)